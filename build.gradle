import net.ltgt.gradle.errorprone.CheckSeverity

plugins {
	id 'java'
	id 'net.ltgt.errorprone' version '1.3.0'
	id 'org.gretty' version '3.0.3'
	id 'dev.jacomet.logging-capabilities' version '0.9.0'
	id 'com.autonomousapps.dependency-analysis' version '0.70.0'
}

repositories {
	mavenCentral()
	jcenter()
}

ext.errorProneVersion = '2.4.0'
ext.shiroVersion = '1.7.0'
ext.jerseyVersion = '2.32'

def webAppDirName = "${projectDir}/src/web/webapp"
project.metaClass.webAppDir = file(webAppDirName) // this is needed by Gretty

gretty.servletContainer = 'tomcat9'

dependencies {
	errorprone "com.google.errorprone:error_prone_core:$errorProneVersion"

	compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
	implementation "org.glassfish.jersey.core:jersey-server:$jerseyVersion"
	implementation "org.glassfish.jersey.containers:jersey-container-servlet:$jerseyVersion"
	implementation "org.glassfish.jersey.media:jersey-media-multipart:$jerseyVersion"
	implementation "org.apache.shiro:shiro-core:$shiroVersion"
	implementation "org.apache.shiro:shiro-web:$shiroVersion"
	runtimeOnly 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.3'
	runtimeOnly "org.glassfish.jersey.inject:jersey-hk2:$jerseyVersion"
	runtimeOnly 'org.apache.logging.log4j:log4j-web:2.14.0' // https://logging.apache.org/log4j/2.x/manual/webapp.html
}

tasks.withType(JavaCompile) {
	options.compilerArgs += '-Xlint:all,-processing'
	options.compilerArgs += '-Werror'
	// error-prone compiler arguments:
	options.errorprone {
		check('TypeParameterUnusedInFormals', CheckSeverity.OFF)
		check('CheckReturnValue', CheckSeverity.OFF)
		check('FutureReturnValueIgnored', CheckSeverity.OFF)
		check('GuardedBy', CheckSeverity.OFF)
		check('EmptyBlockTag', CheckSeverity.OFF)
		check('MissingSummary', CheckSeverity.OFF)

		disableWarningsInGeneratedCode = true
	}
}
